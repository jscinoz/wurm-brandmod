defaultTasks "dist"

subprojects  {
  apply plugin: "java"

  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8

  repositories {
    mavenCentral()
    jcenter()

    maven {
      url "http://gotti.no-ip.org/maven/repository"
    }
  }

  dependencies {
    // Also pulls in the specific version of javassist that will be available at
    // runtime
    compile group: "org.gotti.wurmunlimited", name: "server-modlauncher",
            version: "0.20"
  }

  tasks.withType(JavaCompile) {
    options.compilerArgs <<
      "-Werror" <<
      "-Xlint:all" <<
      "-Xlint:-processing" <<
      "-Xlint:-options" <<
      // Needed for Javassist
      "-Xlint:-classfile" <<
      "-Xdiags:verbose"
  }

  group = "so.jscinoz.wurmunlimited.mods"
  version = "0.0.1"

  task dist(type: Zip) {
    dependsOn "jar"

    into(project.name) {
      from {
        file jar.archivePath
      }
    }

    from {
      file "${project.projectDir}/src/${project.name}.properties"
    }
  }

  jar {
    version = null

    manifest {
      attributes(
        "Implementation-Version": project.version
      )
    }
  }
}
