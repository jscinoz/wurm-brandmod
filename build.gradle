defaultTasks "dist"

subprojects  {
  apply plugin: "java"

  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8

  repositories {
    mavenCentral()
    jcenter()

    maven {
      url "http://gotti.no-ip.org/maven/repository"
    }
  }

  tasks.withType(JavaCompile) {
    options.compilerArgs <<
      "-Werror" <<
      "-Xlint:all" <<
      "-Xlint:-processing" <<
      "-Xlint:-options" <<
      // Needed for Javassist
      "-Xlint:-classfile" <<
      "-Xdiags:verbose"
  }

  group = "so.jscinoz.wurmunlimited.mods"
  version = "0.0.1"

  jar {
    version = null

    manifest {
      attributes(
        "Implementation-Version": project.version
      )
    }
  }
}

def modProjects = subprojects - project(":common");

configure(modProjects) {
  def commonProject = project(":common");

  configurations {
    modRuntime {
      description = "Dependencies needed by mods at runtime that aren't provided"

      // Include everything from the runtime config...
      extendsFrom configurations.runtime

      // ... except the mod launcher (& dependencies) itself
      exclude group: "org.gotti.wurmunlimited"
    }
  }

  dependencies {
    compile commonProject

    // Also pulls in the specific version of javassist that will be available at
    // runtime
    compile group: "org.gotti.wurmunlimited", name: "server-modlauncher",
            version: "0.20"
  }

  task dist(type: Zip) {
    dependsOn "jar"

    into(project.name) {
      from {
        file jar.archivePath
      }
    }

    final modClassPath = (
      // Everything in the common directory (the common.jar itself and any deps)
      configurations.modRuntime.collect { "common/${it.name}" } +
      // Our own jar
      "${project.name}/${jar.archiveName}"
    ).join(",")

    from ("${project.projectDir}/src") {
      include "${project.name}.properties"

      // Build the proper classpath entry for the mod
      expand(modClassPath: modClassPath)
    }
  }
}
